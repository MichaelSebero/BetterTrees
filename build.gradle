buildscript {
	repositories {
		maven {
			url = "https://maven.minecraftforge.net"
		}
	}
	dependencies {
		classpath("com.anatawa12.forge:ForgeGradle:2.3-1.0.+") {
			changing = true
		}
	}
}
apply plugin: 'net.minecraftforge.gradle.forge'

//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.


version = "${mc_version}-${mod_version}"
group = package_group
archivesBaseName = mod_name

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
	sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
	version = "${mc_version}-${forge_version}"
	runDir = "run"
	
	replace "1.12.2-9999.9999.9999z", project.version
	replaceIn "ModConstants.java"
	
	//makeObfSourceJar = false
	
	if (project.hasProperty('mappings_version'))
		mappings = project.mappings_version
}

repositories {
	maven {
		url "https://cursemaven.com"
	}
}

dependencies {
	// Compile against full Hwyla jar.
	implementation("curse.maven:hwyla-253449:2568751")

	// Compile against Serene Seasons, but don't use at runtime.
	compileOnly("curse.maven:serene-seasons-291874:2799213")

	// Compile against Aqua Acrobatics, but don't use at runtime.
	compileOnly("curse.maven:aquaacrobatics-321792:4391401")

	// Use Patchouli at runtime only (for testing the guide book, which is exclusively Json).
	runtimeOnly("curse.maven:patchouli-306770:3162874")
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version
	
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}
	
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

tasks.build.dependsOn deobfJar
